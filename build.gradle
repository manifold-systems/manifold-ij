import org.gradle.api.tasks.testing.logging.TestLogEvent
import java.util.concurrent.TimeUnit

plugins {
  id 'org.jetbrains.intellij' version '0.4.9'
}

description = 'Manifold :: IJ'

wrapper {
  gradleVersion = '4.4'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

configurations {
  manifoldAll
  manifoldEp
}

allprojects {
  repositories {
    if(!System.getenv('CI')) {
      mavenLocal()
    }
    jcenter()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }

  apply plugin: 'java'
  apply plugin: 'org.jetbrains.intellij'
}

File destDir = jar.destinationDir
subprojects {
  tasks.withType( Jar ) {
    // make the manifold-jps-plugin.jar file go in this plugin's dir
    destinationDir = file(destDir)
  }
}

jar {
  manifest {
    attributes(
      'Contains-Sources': 'darkj'
    )
  }
}

dependencies {
  compile group: 'systems.manifold', name: 'manifold', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-util', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-darkj', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-image', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-ext', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-strings', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-exceptions', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-preprocessor', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-xml-rt', version: manifoldVersion

  testCompile group: 'junit', name: 'junit', version: '4.12'

  manifoldAll group: 'systems.manifold', name: 'manifold-all', version: manifoldVersion
  manifoldEp group: 'systems.manifold', name: 'manifold-ext-producer-sample', version: manifoldVersion

  compile project('jps-plugin')
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, TimeUnit.SECONDS //always check for new SNAPSHOTs
}

compileJava {
  options.compilerArgs += ['-proc:none', '-Xplugin:Manifold no-bootstrap']
}
compileTestJava {
  options.compilerArgs += ['-proc:none', '-Xplugin:Manifold']
}

String getIjVersion() {
  return System.getProperty('ijVersion') ?: defaultIjVersion
}

intellij {
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA Community Edition 2017.3.4'
  //alternativeIdePath = 'C:\\Program Files\\Android\\Android Studio'
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA Community Edition 2018.3.4'
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA 192.5281.24'
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA 2019.2.2'
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA 2019.3.3'
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA Community Edition 193.3793.14'
  //alternativeIdePath = 'C:\\Program Files\\JetBrains\\IntelliJ IDEA Community Edition 193.5233.102'
  pluginName project.name
  version getIjVersion() // Ordinarily, just use the version number: '15.0.6' or '2016.3'. Alternatively, the precise API level is OK too: 'IC-143.2370.31'
  if( version.contains( "EAP" ) || Double.parseDouble( version.substring(0, version.lastIndexOf('.'))) >= 2019.2 )
  {
    plugins = ['java', 'com.intellij.lang.jsgraphql:2.4.0'] //include PsiViewer & GraphQL JS in sandbox
  }
  else
  {
    plugins = ['com.intellij.lang.jsgraphql:2.4.0'] //include PsiViewer & GraphQL JS in sandbox
  }
}

runIde {
  minHeapSize '1g'
  maxHeapSize '4g'
}

patchPluginXml {
  sinceBuild '201.0'
  untilBuild '202.*'
}

publishPlugin {
  username = System.getenv('JB_PUBLISH_USER')
  password = System.getenv('JB_PUBLISH_PASS')
}

test {
  //uncomment below to show stdout/stderr in console
  //testLogging.showStandardStreams = true

  testLogging {
    events TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED
  }

  //set a system property in the test JVM containing the path to manifold-all (it could be under ~/.gradle/caches or ~/.m2/repository)
  systemProperties = ['path.to.manifold.all':project.configurations.manifoldAll.singleFile.absolutePath,
                      'path.to.manifold.ep':project.configurations.manifoldEp.files.first()]
}
