import org.jetbrains.intellij.IntelliJPlugin

import java.util.concurrent.TimeUnit

plugins {
  id 'org.jetbrains.intellij' version '0.2.17'
}

description = 'Manifold :: IJ'

wrapper {
  gradleVersion = '4.4'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

repositories {
  mavenLocal()
  jcenter()
  maven { url 'https://oss.jfrog.org/artifactory/libs-snapshot' }
}

dependencies {
  compile group: 'systems.manifold', name: 'manifold', version: manifoldVersion
  compile group: 'systems.manifold', name: 'manifold-ext', version: manifoldVersion
  testCompile group: 'junit', name: 'junit', version: '4.12'
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, TimeUnit.SECONDS //always check for new SNAPSHOTs
}

compileJava {
  options.compilerArgs += ['-proc:none', '-Xplugin:Manifold']
}

intellij {
  pluginName project.name
  version System.getenv().getOrDefault('IDEA_VERSION', IntelliJPlugin.DEFAULT_IDEA_VERSION)
}

patchPluginXml {
  sinceBuild '141.1010'
  untilBuild '999.*'
}

publishPlugin {
  username = System.getenv('JB_PUBLISH_USER')
  password = System.getenv('JB_PUBLISH_PASS')
}
