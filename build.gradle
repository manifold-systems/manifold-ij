import org.gradle.api.tasks.testing.logging.TestLogEvent
import java.util.concurrent.TimeUnit

plugins {
  id 'org.jetbrains.intellij' version '1.3.0'
}

description = 'Manifold :: IJ'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}

configurations {
  manifoldAll
  manifoldEp
}

allprojects {
  repositories {
    if(!System.getenv('CI')) {
      mavenLocal()
    }
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
  }

  // this test block fixes an IJ 2021.3 issue where it won't run tests
  test {
    scanForTestClasses false
    include "**/*Test.class"
  }

  apply plugin: 'java'
  apply plugin: 'org.jetbrains.intellij'
}

File destDir = jar.destinationDir
subprojects {
  tasks.withType( Jar ) {
    // make the manifold-jps-plugin.jar file go in this plugin's dir
    destinationDir = file(destDir)
  }
}

jar {
  manifest {
    attributes(
      'Contains-Sources': 'darkj'
    )
  }
}

dependencies {
  implementation group: 'systems.manifold', name: 'manifold', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-util', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-darkj', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-image', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-ext', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-props', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-strings', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-exceptions', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-preprocessor', version: manifoldVersion
  implementation group: 'systems.manifold', name: 'manifold-xml-rt', version: manifoldVersion

  testImplementation group: 'junit', name: 'junit', version: '4.12'

  manifoldAll group: 'systems.manifold', name: 'manifold-all', version: manifoldVersion
  manifoldEp group: 'systems.manifold', name: 'manifold-ext-producer-sample', version: manifoldVersion

  implementation project('jps-plugin')
}

configurations.all {
  resolutionStrategy.cacheChangingModulesFor 0, TimeUnit.SECONDS //always check for new SNAPSHOTs
}

compileJava {
  options.compilerArgs += ['-proc:none', '-Xplugin:Manifold no-bootstrap']
}
compileTestJava {
  options.compilerArgs += ['-proc:none', '-Xplugin:Manifold']
}

String getIjVersion() {
  return System.getProperty('ijVersion') ?: defaultIjVersion
}

intellij {
  pluginName = project.name
  version = getIjVersion()
  plugins = ['java']//, 'com.intellij.lang.jsgraphql:3.0.0']
}

runIde {
  minHeapSize = '1g'
  maxHeapSize = '4g'
// uncomment to override the ide that is run
//  ideDir = new File("C:\\Program Files\\JetBrains\\ideaIU-2021.3")
}

patchPluginXml {
  sinceBuild = '213.0'
  untilBuild = '221.*'
}

buildSearchableOptions {
  enabled = false
}

test {
  //uncomment below to show stdout/stderr in console
  //testLogging.showStandardStreams = true
  testLogging {
    events = [TestLogEvent.PASSED, TestLogEvent.FAILED, TestLogEvent.SKIPPED]
  }

  //set a system property in the test JVM containing the path to manifold-all (it could be under ~/.gradle/caches or ~/.m2/repository)
  systemProperties = ['path.to.manifold.all':project.configurations.manifoldAll.singleFile.absolutePath,
                      'path.to.manifold.ep':project.configurations.manifoldEp.files.first()]
}
